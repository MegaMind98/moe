#lang shplait
import:
  open:
    "class.rhm"
    "inherit.rhm"

type ClassT
| classT(super_name :: Symbol,
         constructor :: Listof(ExpI * Type),
         fields :: Listof(Symbol * Type),
         methods :: Listof(Symbol * MethodT),
         p_methods :: Listof(Symbol * MethodT))

type MethodT
| methodT(arg_type :: Type,
          result_type :: Type,
          body_exp :: ExpI)

type Type
| intT()
| objT(class_name :: Symbol)
| nullT()

// ----------------------------------------

fun type_error(a, msg):
  error(#'typecheck, "no type: " +& a +& " not " +& msg)

fun get_all_field_types(class_name, t_classes):
  if class_name == #'Object
  | []
  | match find(t_classes, class_name)
    | classT(super_name, constructor, fields, methods, p_methods):
        append(get_all_field_types(super_name, t_classes),
               map(snd, fields))

// ----------------------------------------

fun make_find_in_tree(class_items):
  fun (name, class_name, t_classes):
    def t_class = find(t_classes, class_name)
    def items  = class_items(t_class)
    def super_name = classT.super_name(t_class)
    if super_name == #'Object
    | find(items, name)
    | try:
        find(items, name)
        ~catch:
          make_find_in_tree(class_items)(name,
                                         super_name,
                                         t_classes)

def find_field_in_tree = make_find_in_tree(classT.fields)

def find_method_in_tree = make_find_in_tree(classT.methods)

// ----------------------------------------

fun is_subclass(name1, name2, t_classes):
  cond
  | name1 == name2: #true
  | name1 == #'Object: #false
  | ~else:
      match find(t_classes, name1)
      | classT(super_name, constructor, fields, methods, p_methods):
          is_subclass(super_name, name2, t_classes)

fun is_subtype(t1, t2, t_classes):
  match t1
  | objT(name1):
      match t2 
      | objT(name2):
          is_subclass(name1, name2, t_classes)
      | ~else: #false
  | nullT():
      match t2
      | intT():
          #false
      | ~else: #true
  | ~else: t1 == t2

module test:
  def a_t_class = values(#'A, classT(#'Object, [], [], [], []))
  def b_t_class = values(#'B, classT(#'A, [], [], [], []))

  check: is_subclass(#'Object, #'Object, [])
         ~is #true
  check: is_subclass(#'A, #'B, [a_t_class, b_t_class])
         ~is #false
  check: is_subclass(#'B, #'A, [a_t_class, b_t_class])
         ~is #true

  check: is_subtype(nullT(), intT(), [])
         ~is #false
  check: is_subtype(intT(), intT(), [])
         ~is #true
  check: is_subtype(intT(), objT(#'Object), [])
         ~is #false
  check: is_subtype(objT(#'Object), intT(), [])
         ~is #false
  check: is_subtype(objT(#'A), objT(#'B), [a_t_class, b_t_class])
         ~is #false
  check: is_subtype(objT(#'B), objT(#'A), [a_t_class, b_t_class])
         ~is #true

// ----------------------------------------

def typecheck_exp :: (ExpI, Listof(Symbol * ClassT), Type, Type) -> Type:
  fun (exp, t_classes, this_type, arg_type):
    fun recur(exp):
      typecheck_exp(exp, t_classes, this_type, arg_type)
    fun typecheck_nums(l, r):
      match recur(l)
      | intT():
          match recur(r)
          | intT(): intT()
          | ~else: type_error(r, "Int")
      | ~else: type_error(l, "Int")
    match exp
    | nullI(): nullT()
    | intI(n): intT()
    | plusI(l, r): typecheck_nums(l, r)
    | multI(l, r): typecheck_nums(l, r)
    | argI(): arg_type
    | thisI(): this_type
    | newI(class_name, exps):
        def arg_types = map(recur, exps)
        def field_types = get_all_field_types(class_name, t_classes)
        if (length(arg_types) == length(field_types)
              && foldl(fun (b, r): b && r,
                       #true,
                       map2(fun (t1, t2):
                              is_subtype(t1, t2, t_classes),
                            arg_types,
                            field_types)))
        | objT(class_name)
        | type_error(exp, "field type mismatch")
    | getI(obj_exp, field_name):
        match recur(obj_exp):
        | objT(class_name):
            find_field_in_tree(field_name,
                               class_name,
                               t_classes)
        | ~else: type_error(obj_exp, "object")
    | sendI(obj_exp, method_name, arg_exp):
        def obj_type = recur(obj_exp)
        def arg_type = recur(arg_exp)
        match obj_type
        | objT(class_name):
            typecheck_send(class_name, method_name,
                           arg_exp, arg_type,
                           t_classes)
        | ~else:
           type_error(obj_exp, "object")
    | superI(method_name, arg_exp):
        def arg_type = recur(arg_exp)
        def this_class = find(t_classes, objT.class_name(this_type))
        typecheck_send(classT.super_name(this_class),
                       method_name,
                       arg_exp, arg_type,
                       t_classes)
    | castI(symbol_name, exp):
        def exp_type = recur(exp)
        match exp_type
        | intT():
            if symbol_name == #'Int
            | intT()
            | type_error(symbol_name, "invalid cast")
        | objT(class_name):
            if is_subclass(class_name, symbol_name, t_classes)
            | objT(symbol_name)
            | type_error(class_name, "invalid cast")
        | ~else:
            type_error(exp, "invalid cast")
    | ifI(test, arm1, arm2):
        def test_type = recur(test)
        match test_type
        | intT():
            if is_subtype(recur(arm1),
                          recur(arm2),
                          t_classes)
            | recur(arm1)
            | type_error(exp, "invalid arm types")
        | ~else: type_error(test, "test not a number")
    | setI(obj_exp, field_name, field_value):
        def res_field_value = recur(field_value)
        match recur(obj_exp):
        | objT(class_name):
            if is_subtype(find_field_in_tree(field_name,
                                             class_name,
                                             t_classes),
                          res_field_value,
                          t_classes)
            | res_field_value
            | type_error(exp, "valid mutation")
        | ~else: type_error(obj_exp, "object")
    | beginI(exp):
        ....

fun typecheck_send(class_name :: Symbol,
                   method_name :: Symbol,
                   arg_exp :: ExpI,
                   arg_type :: Type,
                   t_classes :: Listof(Symbol * ClassT)):
  match find_method_in_tree(method_name,
                            class_name,
                            t_classes)
  | methodT(arg_type_m, result_type, body_exp):
      if is_subtype(arg_type, arg_type_m, t_classes)
      | result_type
      | type_error(arg_exp, to_string(arg_type_m))

fun typecheck_method(method :: MethodT,
                     this_type :: Type,
                     t_classes :: Listof(Symbol * ClassT)) :: Void:
  match method
  | methodT(arg_type, result_type, body_exp):
      if is_subtype(typecheck_exp(body_exp, t_classes,
                                  this_type, arg_type),
                    result_type,\

                    t_classes)
      | #void
      | type_error(body_exp, to_string(result_type))

#//
fun check_override(method_name :: Symbol,
                   method :: MethodT,
                   this_class :: ClassT,
                   t_classes :: Listof(Symbol * ClassT)):
  def super_name = classT.super_name(this_class)
  def super_method:
    try:
      // Look for method in superclass:
      find_method_in_tree(method_name,
                          super_name,
                          t_classes)
      ~catch:
        // no such method in superclass:
        method
  println(methodT.arg_type(method))
  println(methodT.arg_type(super_method))
  if (methodT.arg_type(method) == methodT.arg_type(super_method)
        && methodT.result_type(method)  == methodT.result_type(super_method))
  | #void
  | error(#'typecheck, "bad override of " +& method_name)

fun typecheck_class(class_name :: Symbol,
                    t_class :: ClassT,
                    t_classes :: Listof(Symbol * ClassT)):
  match t_class
  | classT(super_name, constructor, fields, methods, p_methods):
      def hello = typecheck_constructor(constructor, t_classes, class_name)
      map(fun (m):
            typecheck_method(snd(m), objT(class_name), t_classes),
          append(methods, p_methods))

fun typecheck_constructor(constructor, t_classes, class_name):
  //println(constructor)
  map((fun (n):
        def values(a, b) = n
        typecheck_exp(a, t_classes, objT(class_name), intT())),
      constructor)

fun typecheck(a :: ExpI,
              t_classes :: Listof(Symbol * ClassT)) :: Type:
  begin:
    map(fun (tc):
          typecheck_class(fst(tc), snd(tc), t_classes),
        t_classes)
    typecheck_exp(a, t_classes, objT(#'Object), intT())

// ----------------------------------------

module test:
  def posn_t_class:
    values(#'Posn,
           classT(#'Object,
                  [values(plusI(intI(3), intI(3)), intT()),
                   values(plusI(intI(3), intI(3)), intT())],
                  [values(#'x, intT()), values(#'y, intT())],
                  [
                    values(#'mdist,
                           methodT(intT(), intT(),
                                   plusI(getI(thisI(), #'x), getI(thisI(), #'y)))),
                    values(#'addDist,
                           methodT(objT(#'Posn), intT(),
                                   plusI(sendI(thisI(), #'mdist, intI(0)),
                                         sendI(argI(), #'mdist, intI(0))))),
                    values(#'addDist22,
                           methodT(objT(#'Posn), intT(),
                                   plusI(intI(0), intI(2))))
                  ],
                  []))

  def posn3D_t_class:
    values(#'Posn3D,
           classT(#'Posn,
                  [],
                  [values(#'z, intT())],
                  [values(#'mdist,
                          methodT(intT(), intT(),
                                  plusI(getI(thisI(), #'z),
                                        superI(#'mdist, argI()))))],
                  []))

  def square_t_class :
    values(#'Square,
           classT(#'Object,
                  [],
                  [values(#'topleft, objT(#'Posn))],
                  [],
                  []))

  fun typecheck_posn(a):
    typecheck(a, [posn_t_class, posn3D_t_class, square_t_class])
  
  def new_posn27 = newI(#'Posn, [intI(2), intI(7)])
  def new_posn531 = newI(#'Posn3D, [intI(5), intI(3), intI(1)])

  // bad posn with null argument
  check: typecheck_posn(setI(newI(#'Posn, [nullI(), intI(7)]), #'x, intI(3)))
         ~raises "type mismatch"
  check: typecheck_posn(ifI(intI(2), setI(new_posn27, #'x, intI(5)), nullI()))
         ~raises "invalid arm types"
  check: typecheck_posn(ifI(intI(2), setI(new_posn27, #'x, intI(5)), intI(5)))
         ~is intT()
  // new_posn27.addDist22(null)
  check: typecheck_posn(setI(new_posn27, #'x, nullI()))
         ~raises "valid mutation"
  check: typecheck_posn(setI(new_posn27, #'x, intI(3)))
         ~is intT()
  check: typecheck_posn(sendI(new_posn27, #'addDist22, nullI()))
         ~is intT()
  check: typecheck_posn(ifI(intI(3), intI(0), intI(2)))
         ~is intT()
  check: typecheck_posn(ifI(newI(#'Posn, [intI(2), intI(7)]), intI(0), intI(2)))
         ~raises "test not a number"
  check: typecheck_posn(ifI(intI(3), intI(0), newI(#'Posn, [intI(2), intI(7)])))
         ~raises "invalid arm types"
  check: typecheck_posn(castI(#'Int, newI(#'Posn, [intI(2), intI(7)])))
         ~raises "invalid cast"
  check: typecheck_posn(castI(#'Posn, intI(7)))
         ~raises "invalid cast"
  check: typecheck_posn(castI(#'Posn, newI(#'Posn, [intI(2), intI(7)])))
         ~is objT(#'Posn)
  check: typecheck_posn(castI(#'Posn, newI(#'Posn, [intI(2), intI(7)])))
         ~is objT(#'Posn)
  check: typecheck_posn(castI(#'Posn, newI(#'Posn, [intI(2), intI(7)])))
         ~is objT(#'Posn)
  check: typecheck_posn(castI(#'Posn, newI(#'Posn3D, [intI(0), intI(1), intI(3)])))
         ~is objT(#'Posn)
  check: typecheck_posn(castI(#'Int, intI(0)))
         ~is intT()
  check: typecheck_posn(sendI(new_posn27, #'mdist, intI(0)))
         ~is intT()
  check: typecheck_posn(sendI(new_posn531, #'mdist, intI(0)))
         ~is intT()  
  check: typecheck_posn(sendI(new_posn531, #'addDist, new_posn27))
         ~is intT() 
  check: typecheck_posn(sendI(new_posn27, #'addDist, new_posn531))
         ~is intT()

  check: typecheck_posn(newI(#'Square, [newI(#'Posn, [intI(0), intI(1)])]))
         ~is objT(#'Square)
  check: typecheck_posn(newI(#'Square, [newI(#'Posn3D, [intI(0), intI(1), intI(3)])]))
         ~is objT(#'Square)

  check: typecheck(multI(intI(1), intI(2)),
                   [])
         ~is intT()

  check: typecheck_posn(sendI(intI(10), #'mdist, intI(0)))
         ~raises "no type"
  check: typecheck_posn(sendI(new_posn27, #'mdist, new_posn27))
         ~raises "no type"
  check: typecheck(plusI(intI(1), newI(#'Object, [])),
                   [])
         ~raises "no type"
  check: typecheck(plusI(newI(#'Object, []), intI(1)),
                   [])
         ~raises "no type"
  check: typecheck(plusI(intI(1), newI(#'Object, [intI(1)])),
                   [])
         ~raises "no type"
  check: typecheck(getI(intI(1), #'x),
                   [])
         ~raises "no type"
  // Overriding examples ...
  check: typecheck(intI(10),
                   [posn_t_class,
                    values(#'Other,
                           classT(#'Posn,
                                  [],
                                  [],
                                  [values(#'mdist,
                                          methodT(objT(#'Object), intT(),
                                                  intI(10)))],
                                  []))])
         ~is intT()
  check: typecheck(intI(10),
                   [posn_t_class,
                    values(#'Other,
                           classT(#'Posn,
                                  [],
                                  [],
                                  [values(#'mdist,
                                          methodT(objT(#'Object), objT(#'Object),
                                                  newI(#'Object, [])))],
                                  []))])
         ~is intT()
  check: typecheck(intI(10),
                   [posn_t_class,
                    values(#'Other,
                           classT(#'Posn,
                                  [],
                                  [],
                                  [values(#'mdist,
                                          methodT(objT(#'Object), objT(#'Object),
                                                  intI(10)))],
                                  []))])
         ~raises "no type"
  // End of overriding examples (covariance and contravariance)
  check: typecheck_method(methodT(intT(), objT(#'Object), intI(0)), objT(#'Object), [])
         ~raises "no type"
  check: typecheck(intI(0),
                   [square_t_class,
                    values(#'Cube,
                           classT(#'Square,
                                  [],
                                  [],
                                  [values(#'m,
                                          methodT(intT(), intT(),
                                                  // No such method in superclass:
                                                  superI(#'m, intI(0))))],
                                  []))])
         ~raises "not found"

// ----------------------------------------

def strip_types :: ClassT -> ClassI:
  fun (t_class):
    match t_class
    | classT(super_name, constructor, fields, methods, p_methods):
       classI(super_name,
              beginI(map((fun (n):
                           fst(n)),
                         constructor)),
              map(fst, fields),
              map(fun (m):
                    values(fst(m),
                           match snd(m)
                           | methodT(arg_type, result_type, body_exp):
                               body_exp),
                  methods),
              map(fun (m):
                    values(fst(m),
                           match snd(m)
                           | methodT(arg_type, result_type, body_exp):
                               body_exp),
                  p_methods)
              )

def interp_t :: (ExpI, Listof(Symbol * ClassT)) -> Value:
  fun (a, t_classes):
    interp_i(a,
             map(fun (c):
                   values(fst(c), strip_types(snd(c))),
                 t_classes))

module test:
  fun interp_t_posn(a):
    interp_t(a, [posn_t_class, posn3D_t_class])
  
  check: interp_t_posn(sendI(new_posn27, #'mdist, intI(0)))
         ~is intV(9)
  check: interp_t_posn(sendI(new_posn531, #'mdist, intI(0)))
         ~is intV(9)
  check: interp_t_posn(sendI(new_posn531, #'addDist, new_posn27))
         ~is intV(18)
  check: interp_t_posn(sendI(new_posn27, #'addDist, new_posn531))
         ~is intV(18)
